plugins {
    id 'java'
    id "io.freefair.lombok" version "4.1.6"
}

group 'com.paytar2800'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.722')

    //Update dynamodb only after customDynamoDBMapper changes are not breaking
    implementation 'com.amazonaws:aws-java-sdk-dynamodb'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-log4j2', version: '1.1.0'
    testImplementation group: 'com.amazonaws', name: 'DynamoDBLocal', version: '1.12.0'
    testImplementation group: 'com.almworks.sqlite4java', name: 'sqlite4java', version: '1.0.392'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.3.1'

}


task copyNativeDeps(type: Copy) {
    from(configurations.compile + configurations.testCompile) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into './build/libs'
}

test {
    //exclude '**/*'
    dependsOn copyNativeDeps
    systemProperty "java.library.path", 'build/libs'
}


task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}


build.dependsOn buildZip
